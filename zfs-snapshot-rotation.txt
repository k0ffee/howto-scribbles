#! /usr/local/bin/zsh

#
# This creates ZFS snapshots and deletes them if
# the snapshot count reaches a limit set as an
# argument to this program.
#
# Example crontab entries:
#
# 14    7 * * * root /root/bin/snapshot 14  zroot/ROOT/default
# 14,44 * * * * root /root/bin/snapshot 336 data
#

PATH=/usr/local/bin:/usr/bin:/bin:/sbin
export PATH
set -eu

argv0=$0
function usage {
    print -n "Error: need number of snapshots to keep "
    print    "and name of filesystem to snapshot"
    print -r "Usage: ${argv0##*/} <number> <name>"
    exit 1
}

[[ $# -ne 2 ]] && usage 

keep=$1
rootfs=$2
date=$(date -u +%Y%m%d%H%M)

[[ -z $date ]] && date=unknown
[[ $keep =~ [^[:digit:]] ]] && usage

#
# Creation of snapshots:
#
if zfs list -o name | grep "^$rootfs$" >/dev/null; then
    zfs list -t snapshot -o name -H | grep "^${rootfs}@$date" >/dev/null || \
        zfs snapshot -r -- "${rootfs}@$date"
fi

#
# Destruction of snapshots:
#
zfs list -t snapshot -S creation -o name -H | grep "^$rootfs"@ | \
    sed 1,"$keep"d | grep @ | \
        while read line; do
            zfs destroy -r -- "$line"
        done
